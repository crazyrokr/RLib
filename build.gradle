rootProject.version = "10.0.alpha1"
group = 'javasabr.rlib'

subprojects {
  repositories {
    mavenCentral()
  }

  apply plugin: "java-library"
  apply plugin: "java-test-fixtures"
  apply plugin: "jacoco"

  java {
    toolchain {
      languageVersion = JavaLanguageVersion.of(21)
    }
  }

  javadoc {
    failOnError = false
  }

  test {
    useJUnitPlatform()
    failOnNoDiscoveredTests = false
  }

  dependencies {
    compileOnly libs.jspecify
    compileOnly libs.lombok
    annotationProcessor libs.lombok

    testImplementation libs.junit.api
    testImplementation libs.junit.jupiter.params
    testCompileOnly libs.lombok
    testCompileOnly libs.jspecify
    testRuntimeOnly libs.junit.engine
    testRuntimeOnly libs.junit.platform.launcher
    testAnnotationProcessor libs.lombok
  }

  tasks.withType(JavaCompile).configureEach {
    options.encoding = "UTF-8"
  }

  tasks.withType(Javadoc).configureEach {
    options.encoding = "UTF-8"
  }

  tasks.register("sourcesJar", Jar) {
    dependsOn "classes"
    group "build"
    archiveClassifier = "sources"
    archiveBaseName = jar.archiveBaseName
    from sourceSets.main.allSource
  }

  tasks.register("javadocJar", Jar) {
    dependsOn "javadoc"
    group "build"
    archiveClassifier = "javadoc"
    archiveBaseName = jar.archiveBaseName
    from sourceSets.main.allSource
  }

  configurations {
    testArtifacts.extendsFrom testRuntime
  }

  tasks.register('testJar', Jar) {
    archiveClassifier = "test"
    from sourceSets.test.output
  }

  artifacts {
    testArtifacts testJar
  }

  tasks.withType(Test).configureEach {
    maxParallelForks = Runtime.runtime.availableProcessors()
  }

  jacocoTestReport {
    dependsOn test
    reports {
      xml.required = false
      csv.required = false
      html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
    }
  }
}

wrapper {
  gradleVersion = '9.0.0'
  distributionType = Wrapper.DistributionType.ALL
}
